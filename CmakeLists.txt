cmake_minimum_required(VERSION 3.16)

# Set the project name and version
project(PacificRenderer VERSION 1.0.0)

# Set C++ standard
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

add_executable(${PROJECT_NAME}
    "src/main.cpp"
    # Add other source files here
    "src/core/Scene.cpp"
    "src/core/Geometry.cpp"
    "src/bsdf/diffuse.cpp"
    "src/bsdf/dielectric.cpp"
    "src/core/Registry.cpp"
    "src/core/MathUtils.cpp"
    "src/integrators/direct.cpp"
    "src/integrators/path.cpp"
    "src/integrators/ptracer.cpp"
    "src/integrators/depth.cpp"
    "src/integrators/albedo.cpp"
    "src/integrators/geometric_normal.cpp"
    "src/integrators/ptracer.cpp"
    "src/integrators/bidirectional.cpp"
    "src/core/Integrator.cpp"
    "src/core/Bitmap.cpp"
    "src/utils/stb.cpp"
    "src/core/Shape.cpp"
    "src/lights/point.cpp"
    "src/lights/area.cpp"
    "src/lights/directional.cpp"
    "src/lights/envmap.cpp"
    "src/lights/constant.cpp"
    "src/filters/gaussian.cpp"
    "src/geometry/triangle.cpp"
    "src/geometry/sphere.cpp"
    "src/bsdf/thindielectric.cpp"
    "src/geometry/disk.cpp"
    "src/bsdf/conductor.cpp"
    "src/core/BSDF.cpp"
    "src/core/Emitter.cpp"
    "src/bsdf/roughconductor.cpp"
    "src/bsdf/roughdielectric.cpp"
    "src/bsdf/plastic.cpp"
    "src/bsdf/roughplastic.cpp"
    "src/microfacet/ggx.cpp"
    "src/microfacet/beckmann.cpp"
    "src/texture/bitmap.cpp"
    "src/texture/checkerboard.cpp"
    "src/texture/constant.cpp"
)

target_include_directories(${PROJECT_NAME} PRIVATE 
    ${CMAKE_CURRENT_SOURCE_DIR}/include
)

# Set output directory for binaries
set_target_properties(${PROJECT_NAME} PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin
)

# Set the desired precision
option(DOUBLE_FLOAT "Use double precision floating point" OFF)
if(DOUBLE_FLOAT)
    target_compile_definitions(${PROJECT_NAME} PRIVATE DOUBLE_FLOAT)
endif()

set(BUILD_SHARED_LIBS OFF)

###########################################################################
# zlib

# Disable zlib testing and examples
set(ZLIB_BUILD_TESTING OFF CACHE BOOL " " FORCE)
set(ZLIB_BUILD_SHARED OFF CACHE BOOL " " FORCE)
set(ZLIB_INSTALL OFF CACHE BOOL " " FORCE)

add_subdirectory(ext/zlib)
target_include_directories(${PROJECT_NAME} PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/zlib
    ${CMAKE_CURRENT_BINARY_DIR}/ext/zlib)   # Is this necessary???
target_link_libraries(${PROJECT_NAME} PRIVATE zlibstatic)
###########################################################################

###########################################################################
# pugixml

add_library(pugixml STATIC ext/pugixml/src/pugixml.cpp)
# Add include directory to the library itself
target_include_directories(pugixml PUBLIC 
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/pugixml/src)
target_link_libraries(${PROJECT_NAME} PRIVATE pugixml)

###########################################################################
# GLM

add_subdirectory(ext/glm)

target_link_libraries(${PROJECT_NAME} PRIVATE glm::glm)
###########################################################################

###########################################################################
# CLI11

add_subdirectory(ext/CLI11)

target_link_libraries(${PROJECT_NAME} PRIVATE CLI11::CLI11)
###########################################################################

###########################################################################
# tinyobjloader

if (DOUBLE_FLOAT)
    set(TINYOBJLOADER_USE_DOUBLE ON CACHE BOOL " " FORCE)
endif()

add_subdirectory(ext/tinyobjloader)

target_link_libraries(${PROJECT_NAME} PRIVATE tinyobjloader)
###########################################################################

###########################################################################
# stb_image_write

target_include_directories(${PROJECT_NAME} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/ext/stb)
###########################################################################

###########################################################################
# happly

add_library(happly INTERFACE)
target_include_directories(happly INTERFACE ${CMAKE_SOURCE_DIR}/ext/happly)

target_link_libraries(${PROJECT_NAME} PRIVATE happly)
###########################################################################

###########################################################################
# openexr

add_subdirectory(ext/openexr)

# TODO: Check for problems
if(MSVC)
    target_compile_options(OpenEXRCore PRIVATE /arch:AVX)  # This enables SSE4.1/SSSE3 and more
    target_compile_options(OpenEXR PRIVATE /arch:AVX)  # This enables SSE4.1/SSSE3 and more
else()
    target_compile_options(OpenEXRCore PRIVATE -mssse3 -msse4.1)
    target_compile_options(OpenEXR PRIVATE -mssse3 -msse4.1)
endif()

set(OPENEXR_USE_TBB ON CACHE BOOL " " FORCE)

target_link_libraries(${PROJECT_NAME} PRIVATE OpenEXR::OpenEXR)
###########################################################################
