# Set minimum required version of CMake
cmake_minimum_required(VERSION 3.16)

# Set the project name and version
project(PacificRenderer VERSION 1.0.0)

# Set C++ standard
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Add executable
add_executable(${PROJECT_NAME} 
    "src/main.cpp"
    # Add other source files here
    "src/core/Scene.cpp"
)

# Include directories (if you have header files in include/)
target_include_directories(${PROJECT_NAME} PRIVATE 
    ${CMAKE_CURRENT_SOURCE_DIR}/src
)

# Set output directory for binaries
set_target_properties(${PROJECT_NAME} PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin
)

# Optional: Set startup project for Visual Studio
set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${PROJECT_NAME})

# Optional: Set Visual Studio specific properties
if(MSVC)
    set_property(TARGET ${PROJECT_NAME} PROPERTY VS_DEBUGGER_WORKING_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
    # Enable multi-processor compilation
    target_compile_options(${PROJECT_NAME} PRIVATE "/MP")
endif()

# Set the desired precision
set(DOUBLE_FLOAT OFF)
set(DOUBLE_INT   OFF)
if(DOUBLE_FLOAT)
    add_definitions(-DDOUBLE_FLOAT)
#else
#    remove_definitions(-DDOUBLE_FLOAT)
endif()
if(DOUBLE_INT)
    add_definitions(-DDOUBLE_INT)
#else
#    remove_definitions(-DDOUBLE_INT)
endif()


###########################################################################
# zlib

# Disable zlib testing and examples
set(ZLIB_BUILD_TESTING OFF CACHE BOOL " " FORCE)
set(ZLIB_BUILD_SHARED ON CACHE BOOL " " FORCE)
set(ZLIB_INSTALL OFF CACHE BOOL " " FORCE)

add_subdirectory(ext/zlib)
target_include_directories(${PROJECT_NAME} PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/zlib
    ${CMAKE_CURRENT_BINARY_DIR}/ext/zlib)   # Is this necessary???
target_link_libraries(PacificRenderer PRIVATE zlibstatic)
###########################################################################

###########################################################################
# pugixml

target_include_directories(${PROJECT_NAME} PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}/ext/pugixml/src)
add_library(pugixml STATIC ext/pugixml/src/pugixml.cpp)
target_link_libraries(PacificRenderer PRIVATE pugixml)
###########################################################################

###########################################################################
# nanogui

# Disable building extras we won't need (pure C++ project)
set(NANOGUI_BUILD_EXAMPLES OFF CACHE BOOL " " FORCE)
set(NANOGUI_BUILD_PYTHON   OFF CACHE BOOL " " FORCE)
set(NANOGUI_INSTALL        OFF CACHE BOOL " " FORCE)
# Add the configurations from nanogui
add_subdirectory(ext/nanogui)
# For reliability of parallel build, make the NanoGUI targets dependencies
set_property(TARGET glfw glfw_objects nanogui PROPERTY FOLDER "dependencies")
# Various preprocessor definitions have been generated by NanoGUI
add_definitions(${NANOGUI_EXTRA_DEFS})
# On top of adding the path to nanogui/include, you may need extras
target_include_directories( ${PROJECT_NAME} PRIVATE
    ${NANOGUI_EXTRA_INCS})
# Lastly, additional libraries may have been built for you.  In addition to linking
# against NanoGUI, we need to link against those as well.
target_link_libraries(PacificRenderer PRIVATE nanogui ${NANOGUI_EXTRA_LIBS})
###########################################################################

###########################################################################
# GLM

add_subdirectory(ext/glm)

target_link_libraries(${PROJECT_NAME} PRIVATE glm::glm)
###########################################################################

###########################################################################
# CLI11

add_subdirectory(ext/CLI11)

target_link_libraries(${PROJECT_NAME} PRIVATE CLI11::CLI11)
###########################################################################
